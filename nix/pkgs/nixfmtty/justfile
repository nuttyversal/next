#!/usr/bin/env -S just --justfile

set export
set shell := ["fish", "--no-config", "--command"]

project := `basename (pwd)`

@ci *COMMAND:
	echo "[INFO] Entering the Nuttyverse CI…"
	nix develop \
		--no-update-lock-file \
		--ignore-environment \
		--command {{COMMAND}}

@develop:
	echo "[INFO] Entering $project shell…"
	nix develop --command fish

@build:
	echo "[INFO] Building $project…"
	nix build --no-update-lock-file

@test: build
	echo "[INFO] Testing $project…"
	fish --no-config ./scripts/test.fish

@deploy: test
	echo "[INFO] Deploying $project…"

@clean:
	echo "[INFO] Cleaning $project…"
	rm --force --verbose result

@update:
	echo "[INFO] Updating $project…"
	nix flake update
	nixfmtty flake.lock

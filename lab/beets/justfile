#!/usr/bin/env -S just --justfile

set export
set shell := ["fish", "--no-config", "--command"]

project := `basename (pwd)`

@ci *COMMAND:
	echo "[INFO] Entering the Nuttyverse CI…"
	nix develop \
		--no-update-lock-file \
		--ignore-environment \
		--command {{COMMAND}}

@develop:
	echo "[INFO] Entering $project shell…"
	fish --no-config ./scripts/develop.fish

@install:
	echo "[INFO] Installing $project…"
	ln \
		--interactive \
		--symbolic \
		(pwd)/config.yaml \
		~/.config/beets/config.yaml
	fish --no-config ./scripts/install.fish

@build: install
	echo "[INFO] Building $project…"

@test: build
	echo "[INFO] Testing $project…"
	fish --no-config ./scripts/test.fish

@deploy: test
	echo "[INFO] Deploying $project…"

@clean:
	echo "[INFO] Cleaning $project…"
	rm --force --recursive --verbose .venv/

@update: clean
	echo "[INFO] Updating $project…"
	nix flake update
	nixfmtty flake.lock
	fish --no-config ./scripts/update.fish
